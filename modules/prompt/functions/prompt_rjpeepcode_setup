#
#
# Authors:
#   Raajay Viswanathan <raajay.v@gmail.com>
#   Geoffrey Grosenbach <boss@topfunky.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/LhgmW.png
#

function prompt_rjpeepcode_precmd {
  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_rjpeepcode_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add a hook for calling info functions before each command.
  add-zsh-hook precmd prompt_rjpeepcode_precmd

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'no'
  zstyle ':prezto:module:git:info:action' format ' +%s'
  zstyle ':prezto:module:git:info:branch' format ' %F{8}%b%f'
  zstyle ':prezto:module:git:info:commit' format ' %F{white}%.7c%f'
  zstyle ':prezto:module:git:info:indexed' format ' '
  zstyle ':prezto:module:git:info:unindexed' format ' '
  zstyle ':prezto:module:git:info:untracked' format ' '
  zstyle ':prezto:module:git:info:dirty' format ' %F{8}✗%f'
  zstyle ':prezto:module:git:info:ahead' format ' %F{8}⬆%f'
  zstyle ':prezto:module:git:info:behind' format ' %F{8}⬇%f'
  zstyle ':prezto:module:git:info:keys' format 'prompt' '%b%c%s%D%A%B'

#%{$fg[yellow]%}%n@%m%{$reset_color%} %d "'${git_info[prompt]}'"
  # Define prompts.
  PROMPT="
%{$fg[blue]%}%D{%L:%M:%S %p}%{$reset_color%} %{$fg[red]%}%n@%m%{$reset_color%} %d "'${git_info[prompt]}'"
%(?.%F{green}${1:-☻ }%f.%F{red}${1:-☻ }%f)%B%F{1}❯%F{2}❯%F{2}❯%f%b "
#RPROMPT='%{$fg[blue]%}[%D{%L:%M:%S %p}]%{$reset_color%}'
RPROMPT=''

}

function prompt_rjpeepcode_help {
  cat <<EOH
This prompt's last command exit status symbol is customizable:

  prompt rjpeepcode [<symbol>]

If this option is not provided, the symbol defaults to ☻.
EOH
}

function prompt_rjpeepcode_preview {
  local +h PROMPT='%# '
  local +h RPROMPT=''
  local +h SPROMPT=''

  if (( $# > 0 )); then
    prompt_preview_theme 'rjpeepcode' "$@"
  else
    prompt_preview_theme 'rjpeepcode'
    print
    prompt_preview_theme 'rjpeepcode' "❯"
    print
    prompt_preview_theme 'rjpeepcode' "$"
  fi
}

prompt_rjpeepcode_setup "$@"
